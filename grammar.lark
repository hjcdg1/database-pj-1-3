%import common._STRING_ESC_INNER
%import common.SIGNED_INT       -> INT
%import common.LETTER           -> C
%import common.DIGIT            -> N
%import common.WS
%ignore WS

// Parenthesis
LP : "("
RP : ")"
DQ : "\""
SQ : "'"

// Tokens
STR : DQ _STRING_ESC_INNER DQ | SQ _STRING_ESC_INNER SQ
DATE.9 : N N N N "-" N N "-" N N
IDENTIFIER : C (C | "_")*

// Keywords
CREATE : "create"i
TABLE : "table"i
DROP : "drop"i
EXPLAIN : "explain"i
DESCRIBE : "describe"i
DESC : "desc"i
INSERT : "insert"i
INTO : "into"i
VALUES : "values"i
DELETE : "delete"i
FROM : "from"i
SELECT : "select"i
WHERE : "where"i
SHOW : "show"i
TABLES : "tables"i
UPDATE : "update"i
SET: "set"i
EXIT : "exit"i
TYPE_INT : "int"i
TYPE_CHAR : "char"i
TYPE_DATE : "date"i
NOT : "not"i
NULL : "null"i
PRIMARY : "primary"i
FOREIGN : "foreign"i
KEY : "key"i
REFERENCES : "references"i
AS : "as"i
OR : "or"i
AND : "and"i
LESSTHAN : "<"
LESSEQUAL : "<="
GREATERTHAN : ">"
GREATEREQUAL : ">="
EQUAL : "="
NOTEQUAL : "!="
IS : "is"i

// QUERY
command : query ";"
query : create_table_query
      | drop_table_query
      | explain_query
      | describe_query
      | desc_query
      | insert_query
      | delete_query
      | select_query
      | show_tables_query
      | update_query
      | exit_query

// CREATE TABLE
create_table_query : CREATE TABLE table_name LP table_element_list RP
table_name : IDENTIFIER
table_element_list : table_element ("," table_element)*
table_element : column_definition
              | integrity_constraint_definition
column_definition : column_name data_type [NOT NULL]
column_name : IDENTIFIER
data_type : TYPE_INT
          | TYPE_CHAR LP INT RP
          | TYPE_DATE
integrity_constraint_definition : primary_key_definition
                                | foreign_key_definition
primary_key_definition : PRIMARY KEY LP column_name_list RP
foreign_key_definition : FOREIGN KEY LP column_name_list RP REFERENCES table_name LP column_name_list RP
column_name_list : column_name ("," column_name)*

// DROP TABLE
drop_table_query : DROP TABLE table_name

// EXPLAIN
explain_query : EXPLAIN table_name

// DESCRIBE
describe_query : DESCRIBE table_name

// DESC
desc_query : DESC table_name

// INSERT
insert_query : INSERT INTO table_name [LP column_name_list RP] VALUES LP column_value_list RP
column_value_list : column_value ("," column_value)*
column_value : INT | STR | DATE | NULL

// DELETE
delete_query : DELETE FROM table_name [WHERE boolean_expr]

// SELECT
select_query : SELECT column_expr_list FROM table_expr_list [WHERE boolean_expr]
column_expr_list : "*"
                 | column_expr ("," column_expr)*
column_expr : [table_name "."] column_name [AS column_name]
table_expr_list : table_expr ("," table_expr)*
table_expr : table_name [AS table_name]
boolean_expr : boolean_term (OR boolean_term)*
boolean_term : boolean_factor (AND boolean_factor)*
boolean_factor : [NOT] boolean_test
boolean_test : predicate
             | parenthesized_boolean_expr
predicate : comparison_predicate
          | null_predicate
comparison_predicate : comp_operand comp_op comp_operand
comp_operand : comparable_value
             | [table_name "."] column_name
comparable_value : INT | STR | DATE
comp_op : LESSTHAN | LESSEQUAL | EQUAL | GREATERTHAN | GREATEREQUAL | NOTEQUAL
null_predicate : [table_name "."] column_name null_operation
null_operation : IS [NOT] NULL
parenthesized_boolean_expr : LP boolean_expr RP

// SHOW TABLES
show_tables_query : SHOW TABLES

// UPDATE
update_query : UPDATE table_name SET set_column_list [WHERE boolean_expr]
set_column_list : set_column ("," set_column)*
set_column : column_name EQUAL column_value

// EXIT
exit_query : EXIT
